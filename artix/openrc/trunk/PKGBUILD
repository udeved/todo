# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgbase=openrc
pkgname=('openrc' 'openrc-sysvcompat' 'openrc-bash-completions' 'openrc-zsh-completions')
pkgver=0.35.5
pkgrel=3
pkgdesc="Gentoo's universal init system"
arch=('i686' 'x86_64' 'armv7h')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
makedepends=('psmisc' 'pam' 'opentmpfiles' 'zsh')
options=('!emptydirs')
source=("${pkgbase}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
        "${pkgbase}.logrotate"
        'openrc-install.hook'
        'openrc-remove.hook')
sha256sums=('502c14044ff587cbabc943e2fde1e4679cce0936c6d0eabd7a5d15609f6d9c72'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '1d4d31a7275660e7b23778e6fceb0714cab8ed2793b04aa004b01d8e7bcd43bd'
            'cbb4d00262a55a508114cb31e3903350be2cff748a4fb5e120dc745292e3987e')

_args=(
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=yes
    MKBASHCOMP=yes
    MKZSHCOMP=yes
    MKPKGCONFIG=yes
)

if [ -f /usr/lib/os-release ]; then
    . /usr/lib/os-release
    _args+=(BRANDING="${NAME}")
else
    _args+=(BRANDING='Unknown Linux')
fi

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's:0444:0644:' mk/sys.mk
}

build(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make "${_args[@]}"
}

package_openrc() {
    depends=('psmisc' 'pam' 'opentmpfiles' 'openrc-sysvcompat')
    optdepends=('netifrc: Network Interface Management Scripts'
                'networkmanager-openrc: NetworkManager')
    install=${pkgname}.install
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    cd "${srcdir}/${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgbase}"

    sed -e 's/#unicode="NO"/unicode="YES"/' \
        -e 's/#rc_logger="NO"/rc_logger="YES"/' \
        -i "${pkgdir}/etc/rc.conf"

    install -d ${pkgdir}/usr/lib/openrc/cache

    # getty
    sed -e 's|#baud=""|baud="38400"|' \
        -e 's|#term_type="linux"|term_type="linux"|' \
        -e 's|#agetty_options=""|agetty_options=""|' \
        -i ${pkgdir}/etc/conf.d/agetty

    for num in 1 2 3 4 5 6;do
        cp -v ${pkgdir}/etc/conf.d/agetty ${pkgdir}/etc/conf.d/agetty.tty$num
        ln -sf /etc/init.d/agetty ${pkgdir}/etc/init.d/agetty.tty$num
        ln -sf /etc/init.d/agetty.tty$num ${pkgdir}/etc/runlevels/default/agetty.tty$num
    done

    # alpm hooks
    for h in openrc-{install,remove}.hook;do
        install -Dm644 ${srcdir}/$h ${pkgdir}/usr/share/libalpm/hooks/$h
    done

    # license
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgbase}/LICENSE"

    # split completions
    install -d ${srcdir}/_bash-completion/usr/share/bash-completion/completions
    mv -v ${pkgdir}/usr/share/bash-completion/completions/* ${srcdir}/_bash-completion/usr/share/bash-completion/completions
    install -d ${srcdir}/_zsh/usr/share/zsh/site-functions
    mv -v ${pkgdir}/usr/share/zsh/site-functions/* ${srcdir}/_zsh/usr/share/zsh/site-functions

    # split sysvinit compat
    install -d ${srcdir}/_sysvinit_compat/usr/{lib/openrc/bin,bin,share/man/man8}
    mv -v ${pkgdir}/usr/lib/openrc/bin/{halt,poweroff,reboot,shutdown} ${srcdir}/_sysvinit_compat/usr/lib/openrc/bin
    mv -v ${pkgdir}/usr/bin/{openrc-init,openrc-shutdown,init,halt,poweroff,reboot,shutdown} ${srcdir}/_sysvinit_compat/usr/bin
    mv -v ${pkgdir}/usr/share/man/man8/{openrc-init,openrc-shutdown}.8 ${srcdir}/_sysvinit_compat/usr/share/man/man8
#     mv -v ${pkgdir}/usr/lib/lib{einfo,rc}.so* ${srcdir}/_sysvinit_compat/usr/lib
}

package_openrc-sysvcompat(){
    pkgdesc="Openrc sysvinit compatibility"
    depends=('sh')
    conflicts=('sysvinit' 'systemd-sysvcompat')
    replaces=('sysvinit')

    mv -v ${srcdir}/_sysvinit_compat/* ${pkgdir}
}

package_openrc-bash-completions() {
    depends=('openrc' 'bash')
    pkgdesc="OpenRC completions for Bash"

    mv -v ${srcdir}/_bash-completion/* ${pkgdir}
}

package_openrc-zsh-completions() {
    depends=('openrc' 'zsh')
    pkgdesc="OpenRC completions for ZSH"

    mv -v ${srcdir}/_zsh/* ${pkgdir}
}
