# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=prometheus-openrc
pkgver=20180312
pkgrel=1
pkgdesc="OpenRC prometheus init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'prometheus')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/prometheus')
source=("prometheus.confd::${_url}/net-analyzer/prometheus/files/prometheus.confd"
       "prometheus.initd::${_url}/net-analyzer/prometheus/files/prometheus-3.initd")
sha256sums=('940f1a3ad08f0eb370951b1a42c29a9217cba9ae2f5cb6cc675bdec1d51e8309'
            '3f17a26f1bf3e16e6828ecce6b8919a7f91f54ed88092d2c2b943b1a70b8fcb4')

# pkgver() {
# 	date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -e "s/:-config\.file/:--config\.file/g" \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'prometheus'
    _inst_initd 'prometheus'
}
