# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=rsyslog-openrc
pkgver=20180312
pkgrel=1
pkgdesc="OpenRC rsyslog init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'rsyslog')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/rsyslog')
source=("rsyslog.confd::${_url}/app-admin/rsyslog/files/8-stable/rsyslog.confd-r1"
        "rsyslog.initd::${_url}/app-admin/rsyslog/files/8-stable/rsyslog.initd-r1")
sha256sums=('0be3575df8010523de6b37e98c417192e8ec481292ca9fb47c8d2211795e03a6'
            '93044cc5fc344cd90d17cbb5cebee3c60ebf674e53c8b9efc6e758d53912d343')

# pkgver() {
#     date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'rsyslog'
    _inst_initd 'rsyslog'
}
