# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=acpid-openrc
pkgver=20180305
pkgrel=1
pkgdesc="OpenRC acpid init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'acpid')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/acpid')
source=("acpid.confd::${_url}/sys-power/acpid/files/acpid-2.0.16-conf.d"
        "acpid.initd::${_url}/sys-power/acpid/files/acpid-2.0.26-init.d")
sha256sums=('3755d4eb8bb64a1304e5defedb949305ac550565da36fe4f94d5f31beee821ba'
            '5dcf5721a129b3782c13fc453853c390c34e2ad4ee00706388ce014454b063ee')

# pkgver() {
# 	date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'acpid'
    _inst_initd 'acpid'
}
