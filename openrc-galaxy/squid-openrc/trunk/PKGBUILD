# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=squid-openrc
pkgver=20180312
pkgrel=1
pkgdesc="OpenRC squid init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'squid')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/squid')
source=("squid.confd::${_url}/net-proxy/squid/files/squid.confd-r1"
        "squid.initd::${_url}/net-proxy/squid/files/squid.initd-r4")
sha256sums=('507d8f26bdb7227c39ed0641429d32972513e4240bf59a20b076e8019201b20d'
            '8a1369aab5cef61d19ea61c373819bd4eeabd2ba4c70f831e5009891993f3a4f')

# pkgver() {
#     date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -e 's|chown squid|chown proxy|' \
        -e 's|squid:squid|proxy:proxy|' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'squid'
    _inst_initd 'squid'
}
