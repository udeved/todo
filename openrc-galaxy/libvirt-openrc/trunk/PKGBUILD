# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=libvirt-openrc
pkgver=20180312
pkgrel=1
pkgdesc="OpenRC libvirt init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'libvirt')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/libvirtd')
source=("libvirtd.confd::${_url}/app-emulation/libvirt/files/libvirtd.confd-r5"
        "libvirtd.initd::${_url}/app-emulation/libvirt/files/libvirtd.init-r16"
        "virtlockd.initd::${_url}/app-emulation/libvirt/files/virtlockd.init-r1"
        "virtlogd.initd::${_url}/app-emulation/libvirt/files/virtlogd.init-r1"
        "libvirt-guests.confd::${_url}/app-emulation/libvirt/files/libvirt-guests.confd"
        "libvirt-guests.initd::${_url}/app-emulation/libvirt/files/libvirt-guests.init-r2")
sha256sums=('4f7fba7e64533868119c0f3355aa22932e163b208397323dc2cd96daadcc4079'
            '1f875e12cc6ecb1b434bfea774893b84f6602072da0218b654d2ecf79cf2d36d'
            '23ddea66fb2d85c17c382daa07abfabe7fa57d62406d2ca5df0c755f5dcc3834'
            '1647c11779fd874bdc115234bae0f0f65278084541473d2825aa9864ad49f939'
            'd5f85bb8c1d2010347f23badc422e98046b97a0066254739b5829fce07837d63'
            'f78ced5ebe644522c6ce102b3f6bcf932686243a50629559f988548c570cad3a')

# pkgver() {
# 	date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'libvirtd'
    _inst_initd 'libvirtd'
    _inst_initd 'virtlockd'
    _inst_initd 'virtlogd'

    _inst_confd 'libvirt-guests'
    _inst_initd 'libvirt-guests'

    sed -e "s/USE_FLAG_FIREWALLD/need firewalld/" \
        -e "s/USE_FLAG_AVAHI/use avahi-daemon/" \
        -e "s/USE_FLAG_ISCSI/use iscsid/" \
        -e "s/USE_FLAG_RBD/use ceph/" \
        -i "${pkgdir}/etc/init.d/libvirtd"
}

