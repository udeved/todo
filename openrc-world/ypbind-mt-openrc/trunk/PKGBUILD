# Maintainer: artoo <artoo@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=ypbind-mt-openrc
pkgver=20171213
pkgrel=1
pkgdesc="OpenRC ypbind-mt init script"
arch=('any')
url="https://github.com/artix-linux/packages"
license=('GPL2')
groups=('openrc-world')
depends=('ypbind-mt' 'rpcbind-openrc' 'openslp-openrc')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/ypbind')
source=("ypbind.confd::${_url}/net-nds/ypbind/files/ypbind.confd-r1"
        "ypbind.initd::${_url}/net-nds/ypbind/files/ypbind.initd")
sha256sums=('79f6c6ed4875f00fe702f69e9a6180a032d5a47f3e58e892e6ba0e2746b6fec8'
            'fad2c4c9927edd6a3b7479dba733f67c5cfef399de133c1b4a69ac0526692dbd')

pkgver() {
    date +%Y%m%d
}

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'ypbind'
    _inst_initd 'ypbind'
}
